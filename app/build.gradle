apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "br.com.aisdigital.androidchallenge"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    args "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.core:core-ktx:$versions.ktx"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint"

    //architecture components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.liveData"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.liveData"
    implementation "androidx.lifecycle:lifecycle-runtime:$versions.liveData"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation "com.airbnb.android:lottie:$versions.lottie"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.cardview:cardview:$versions.cardview"

    //test
    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"

    testImplementation "io.mockk:mockk:$versions.mockk"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
    testImplementation "androidx.test:core:$versions.androidXTestCore"

    //retrofit"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.logging"

    //lint
    ktlint "com.pinterest:ktlint:$versions.ktlint"

    //dependency injection
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-android-scope:$versions.koin"
    implementation "org.koin:koin-android-viewmodel:$versions.koin"

    //coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.courotines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.courotines"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$versions.courotinesAdapter"
}
